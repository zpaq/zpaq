(zpaq 0.04 file tuned for high compression (slow)
on the Calgary corpus. Uses 261 MB memory)

comp 5 9 0 3 22 (hh hm ph pm n)
  0 const 160
  1 icm 5  (orders 0-6)
  2 isse 13 1 48 16 (sizebits j c rate)
  3 isse 16 2 48 16
  4 isse 18 3 52 14
  5 isse 19 4 48 14
  6 isse 20 5 48 12
  7 isse 20 6 44 10
  8 match 22
  9 icm 17 (order 0 word)
  10 isse 19 9 48 10 (order 1 word)
  11 icm 10 (sparse with gaps 1-3)
  12 icm 10
  13 icm 10
  14 icm 14 (pic)
  15 mix 16 0 15 24 255 (mix orders 1 and 0)
  16 mix 8 0 16 10 255 (including last mixer)
  17 mix2 0 15 16 24 0
  18 sse 8 17 32 255 255 (order 0)
  19 mix2 8 17 18 16 255
  20 sse 16 19 32 255 255 (order 1)
  21 mix2 0 19 20 16 0
hcomp
  c++ *c=a b=c a=0 (save in rotating buffer)
  d= 2 hash *d=a b-- (orders 1,2,3,4,5,7)
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash b-- hash *d=a b--
  d++ hash *d=a b-- (match, order 8)
  d++ a=*c a&~ 32 (lowercase words)
    a< 65 jt 14 a> 90 jt 10
    d++ hashd d-- (added: update order 1 word hash)
    *d<>a a+=*d a*= 20 *d=a jmp 9
    a=*d a== 0 jt 3 (order 1 word)
       d++ *d=a d--
    *d=0 d++
  d++ b=c b-- a=0 hash *d=a (sparse 2)
  d++ b-- a=0 hash *d=a (sparse 3)
  d++ b-- a=0 hash *d=a (sparse 4)
  d++ a=b a-= 212 b=a a=0 hash
    *d=a b<>a a-= 216 b<>a a=*b a&= 60 hashd (pic)
  d++ a=*c a<<= 9 *d=a (mix)
  d++
  d++
  d++ d++
  d++ *d=a (sse)
  halt
post
  0  (may be 0 for PASS or x for EXE/DLL (E8E9))
     (if x, set ph=0, pm=3)
end
