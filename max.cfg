(zpaq 0.04 file tuned for high compression (slow)
on the Calgary corpus. Uses 322 MB memory)

comp 5 9 0 3 21 (hh hm ph pm n)
  0 icm 5  (orders 0-6)
  1 icm 10
  2 icm 16
  3 icm 19
  4 icm 19
  5 icm 19
  6 icm 20
  7 match 23 (order 7)
  8 icm 19 (order 0 word)
  9 icm 19 (order 1 word)
  10 icm 10 (sparse with gaps 1-3)
  11 icm 10
  12 icm 10
  13 icm 16 (pic)
  14 mix 16 0 14 24 255 (mix orders 1 and 0)
  15 mix 8 0 15 10 255 (including last mixer)
  16 avg 14 15 64 (average of both mixers)
  17 sse 8 16 32 255 255 (order 0)
  18 avg 16 17 64
  19 sse 16 18 32 255 255 (order 1)
  20 avg 18 19 96
hcomp
  c++ *c=a (save in rotating buffer)
  d=0 b=c a=0
  d++ hash *d=a b-- (orders 1-6)
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b-- (match)
  d++ a=*c a&~ 32 (lowercase words)
    a< 65 jt 11 a> 90 jt 7
    *d<>a a+=*d a*= 20 *d=a jmp 9
    a=*d a== 0 jt 3 (order 1 word)
       d++ *d=a d--
    *d=0 d++
  d++ b=c b-- a=0 hash *d=a (sparse 2)
  d++ b-- a=0 hash *d=a (sparse 3)
  d++ b-- a=0 hash *d=a (sparse 4)
  d++ a=b a-= 212 b=a a=0 hash
    *d=a b<>a a-= 216 b<>a a=*b a&= 60 hashd (pic)
  d++ a=*c a<<= 9 *d=a (mix)
  d++
  d++
  d++ d++
  d++ *d=a (sse)
  halt
post
  0  (may be 0 for PASS or x for EXE/DLL (E8E9))
     (if x, set ph=0, pm=3)
end
