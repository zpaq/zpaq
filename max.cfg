(zpaq 0.01 file tuned for high compression (slow)
on the Calgary corpus. Uses 93 MB memory)

comp 4 9 0 0 19
  0 icm 5
  1 icm 10
  2 icm 14
  3 icm 17
  4 icm 17
  5 icm 17
  6 icm 18
  7 match 21
  8 icm 17
  9 icm 17
  10 icm 10
  11 icm 10
  12 icm 10
  13 icm 16
  14 mix 16 0 14 16 255
  15 mix 8 0 15 16 255
  16 avg 14 15 128
  17 sse 16 16 1 64 255
  18 avg 16 17 160
hcomp
  c++ *c=a (save in rotating buffer)
  d=0 b=c a=0
  d++ hash *d=a b-- (orders 1-6)
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b--
  d++ hash *d=a b-- (match)
  d++ a=*c a&~32 (lowercase words)
    a<65 jt 11 a>90 jt 7
    *d<>a a+=*d a*=20 *d=a jmp 9
    a=*d a==0 jt 3 (order 1 word)
       d++ *d=a d--
    *d=0 d++
  d++ b=c b-- a=0 hash *d=a (sparse 2)
  d++ b-- a=0 hash *d=a (sparse 3)
  d++ b-- a=0 hash *d=a (sparse 4)
  d++ a=b a-=212 b=a a=0 hash
    *d=a b<>a a-=216 b<>a a=*b a&=60 hashd (pic)
  d++ a=*c a<<=8 *d=a (mix)
  d++
  d++
  d++ *d=a (sse)
  halt
post
  pass
end
