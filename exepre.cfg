(preprocessor for exe.cfg. To run: zpaq rexepre.cfg)
(This is not a config file for compression)
comp 0 3 0 0 0
hcomp
  (inverse E8E9 transform)
  a> 255 ifnot
    *b=a (save input)
    a=b a-=c a== 4 ifnot
      b++ halt (buffer not full)
    endif
    a=*c a&= 254 a== 232 ifnot
      a=*c out c++ b++ halt (buffer full and not E8/E9)
    endif
    a=*b b-- a<<= 8 a+=*b b-- a<<= 8 a+=*b b-- a<<= 8 a+=*b (load addr)
    a+=c (convert to absolute address)
    *b=a a>>= 8 b++ *b=a a>>= 8 b++ *b=a a>>= 8 b++ *b=a (save addr)
    a=*c out c++  a=*c out c++  a=*c out c++  a=*c out c++  a=*c out c++
    b++
    halt
  endif
  do 
    a=b a==c ifnot
      a=*c out c++
    forever
  endif
  b=0 c=0 halt
post 0 end
